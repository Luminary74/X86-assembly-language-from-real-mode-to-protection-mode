## c09_2.asm



```
         ;代码清单9-2
         ;文件名：c09_2.asm
         ;文件说明：用于演示BIOS中断的用户程序 
         ;创建日期：2012-3-28 20:35
         
;===============================================================================
SECTION header vstart=0                     ;定义用户程序头部段 
    program_length  dd program_end          ;程序总长度[0x00]
    
    ;用户程序入口点
    code_entry      dw start                ;偏移地址[0x04]
                    dd section.code.start   ;段地址[0x06] 
    
    realloc_tbl_len dw (header_end-realloc_begin)/4
                                            ;段重定位表项个数[0x0a]
    
    realloc_begin:
    ;段重定位表           
    code_segment    dd section.code.start   ;[0x0c]
    data_segment    dd section.data.start   ;[0x14]
    stack_segment   dd section.stack.start  ;[0x1c]
    
header_end:                
    
;===============================================================================
SECTION code align=16 vstart=0           ;定义代码段（16字节对齐） 
start:
      mov ax,[stack_segment]
      mov ss,ax
      mov sp,ss_pointer
      mov ax,[data_segment]
      mov ds,ax
      
      mov cx,msg_end-message
      mov bx,message
      
 .putc:
      mov ah,0x0e
      mov al,[bx]
      int 0x10
      inc bx
      loop .putc

 .reps:
      mov ah,0x00
      int 0x16
      
      mov ah,0x0e
      mov bl,0x07
      int 0x10

      jmp .reps

;===============================================================================
SECTION data align=16 vstart=0

    message       db 'Hello, friend!',0x0d,0x0a
                  db 'This simple procedure used to demonstrate '
                  db 'the BIOS interrupt.',0x0d,0x0a
                  db 'Please press the keys on the keyboard ->'
    msg_end:
                   
;===============================================================================
SECTION stack align=16 vstart=0
           
                 resb 256
ss_pointer:
 
;===============================================================================
SECTION program_trail
program_end:
```



### 程序实现功能

键盘读字符并显示



前面，正常定义用户程序头部信息、用户程序入口点、段重定位表



定义代码段

### 使用BIOS中断向屏幕写字符

```
 mov cx,msg_end-message
      mov bx,message
      
 .putc:
      mov ah,0x0e
      mov al,[bx]
      int 0x10
      inc bx
      loop .putc
```

首先，把重复次数传入CX，然后让BX指向要显示信息的首地址

然后我们要利用0x10号中断的0x0e号功能

```
BIOS中断显示服务(Video Service——INT 10H)

功能描述：在Teletype模式下显示字符，具体说就是在屏幕的光标处写一个字符，并推进光标的位置。
入口参数：

AH＝0EH
AL＝字符
BH＝页码
BL＝前景色(图形模式)；注意，仅在图形模式下，设置BL才会改变前景色；在文本模式下，这个参数不起作用（我们的实验工作在文本模式下）
出口参数：无
```

### 使用BIOS中断从键盘读取字符

```
 .reps:
      mov ah,0x00
      int 0x16
      
      mov ah,0x0e
      mov bl,0x07
      int 0x10

      jmp .reps
```

0x16号中断的0x00号子功能就是从键盘读字符

再次利用0x10号中断的0x0e号功能，把我们从键盘输入的字符显示出来

```
BIOS中断键盘服务(Keyboard Service——INT 16H)
功能描述：从键盘读入字符
入口参数：

AH＝00H——读键盘
AH＝10H——读扩展键盘（可根据0000:0496H单元的内容判断：扩展键盘是否有效 ）
出口参数：

AH＝键盘的扫描码
AL＝字符的ASCII码
```

